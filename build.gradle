buildscript {
	ext {
		springBootVersion = '3.1.2'
		dependencyManagementVersion = '1.1.2'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}"
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.cat-paw'
	version = '0.0.1-SNAPSHOT'

	java {
		sourceCompatibility = '17'
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.boot:spring-boot-starter-validation'

		runtimeOnly 'org.postgresql:postgresql'

		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testCompileOnly 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

project(':core-module') {
	bootJar { enabled = false }
	jar { enabled = true }

	dependencies {
	}
}

project(':api-module') {
	bootJar { enabled = true }
	jar { enabled = false }

	dependencies {
		implementation project(':core-module')
	}
}

project(':chat-module') {
	bootJar { enabled = true }
	jar { enabled = false }

	dependencies {
		implementation project(':core-module')
	}
}